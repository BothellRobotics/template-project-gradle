ssh.settings {
    knownHosts = allowAnyHosts
}

remotes {
    ev3dev {
        host = project.brickHost
        user = project.brickUser
        password = project.brickPassword
    }
}

// Directory paths
project.ext.externalJarsPath = "/home/robot/java/libraries"
project.ext.userJarsPath = "/home/robot/java/programs"
project.ext.userWrappersPath = "/home/robot"

project.ext.userJarPath = { fat = true ->
    def suffix = fat ? "-all" : "";
    return "${project.userJarsPath}/${rootProject.name}-${version}${suffix}.jar";
}

project.ext.userWrapperPath = { fat = true ->
    def suffix = fat ? "-all" : "";
    return "${project.userWrappersPath}/${rootProject.name}-${version}${suffix}.sh";
}

ext.baseGroup = "ELJ-Basic"
ext.fatGroup = "ELJ-FatJar"
ext.slimGroup = "ELJ-SlimJar"

//////////////////
// TASK HELPERS //
//////////////////

/////////////////////////////////
// Get classpath for a Slim JAR
def getSlimClassPath() {
    def dependenciesPath = "${project.userJarPath(false)}"

    configurations.runtime.each {
        dependenciesPath += ":${project.externalJarsPath}/${it.name}"
    }

    return dependenciesPath
}

////////////////////////////////////////
// Get command line for java execution
// named arguments:
//  - sudo: whether prepend java with sudo
//  - time: whether prepend java with time
//  - brickrun: whether to prepend java with brickrun
//  - verboseClass: whether to debug class loading
//  - jmx: whether to activate the management agent
//  - slim: JAR type
def getJavaCommand(Map map) {
    def cmdLine = ""
    if (map.sudo) {
        cmdLine += "echo -e \"${project.brickPassword}\" | sudo -S "
    }
    if (map.time) {
        cmdLine += "time "
    }
    if (map.brickrun) {
        cmdLine += "brickrun -- "
    }
    cmdLine += "java "
    if (map.verboseClass) {
        cmdLine += "-Xlog:class+load=info,class+unload=info "
    }
    if (map.jmx) {
        cmdLine += jmxFlags + " "
    }
    if (map.slim) {
        cmdLine += "-cp \"${getSlimClassPath()}\" ${project.mainClass}"
    } else {
        cmdLine += "-jar ${project.userJarPath(true)}"
    }
    return cmdLine
}

////////////////////////////////////////
// Get command line for java execution
// normal arguments:
//  - deploy: whether to prepend the name with deployAnd or remote
// named arguments:
//  - sudo: add Sudo to name
//  - brickrun: add Brick to name
//  - verboseClass: add Classload to name
//  - jmx: add Profiling to name
//  - slim: add Slim to name
def getTaskName(Map map, deploy = false) {
    def name = ""

    if (deploy) {
        name += "deployAnd"
    } else {
        name += "remote"
    }

    if (map.jmx) {
        name += "Profiling"
    }
    if (map.sudo) {
        name += "Sudo"
    }
    if (map.brickrun) {
        name += "Brick"
    }
    name += "Run"
    if (map.verboseClass) {
        name += "Classload"
    }
    if (map.slim) {
        name += "Slim";
    }
    return name
}

/////////////////////////////////
// Get name for deployment task
// named arguments:
//  - slim: whether to include Slim in name
def getDeployName(Map map) {
    if (map.slim) {
        return "deploySlim"
    } else {
        return "deploy"
    }
}

/////////////////////
// WRAPPER SUPPORT //
/////////////////////

task templateWrapperSlim(type: Copy) {
    from "${project.rootDir}/gradle/"
    into "${project.rootDir}/build/"
    include 'launcher-simple.sh.template'
    rename { file -> 'launcher.sh' }
    expand(mode: "jarpath", java_opts: "$jvmFlags", classpath: "${getSlimClassPath()}", mainclass: "$mainClass")
}

task templateWrapperFat(type: Copy) {
    from "${project.rootDir}/gradle/"
    into "${project.rootDir}/build/"
    include 'launcher-simple.sh.template'
    rename { file -> 'launcher.sh' }
    expand(mode: "jarmain", java_opts: "$jvmFlags", jar: "${userJarPath(true)}")
}

///////////////////
// TASK CREATION //
///////////////////

//////////////////////
// Create a Run task
// named arguments:
//  - sudo: is this a sudo run?
//  - time: is this a timed run?
//  - brickrun: is this a brickrun run?
//  - verboseClass: is class loading debug wanted?
//  - jmx: is JMX agent activation wanted?
//  - slim: deployment type
def createRunTask(Map map) {
    def taskGroup = map.slim ? rootProject.slimGroup : rootProject.fatGroup

    def onlyRunName = getTaskName(map, false)
    def andDeployName = getTaskName(map, true)

    task "$andDeployName" {
        group "$taskGroup"
        dependsOn "${getDeployName(map)}", "$onlyRunName"
        doLast {}
    }

    task "$onlyRunName" {
        group "$taskGroup"
        doLast {
            ssh.run {
                session(remotes.ev3dev) {
                    sshPrint(delegate, "${getJavaCommand(map)}}")
                }
            }
        }
    }
}

/////////////////////////
// Create a Deploy task
// named arguments:
//  - slim: deployment type
def createDeployTask(Map map) {
    def wrapperPath = userWrapperPath(!map.slim)

    def taskGroup, artifactType, wrapperTask, jarName
    if (map.slim) {
        taskGroup = rootProject.slimGroup
        artifactType = "jar"
        wrapperTask = "templateWrapperSlim"
        jarName = "${rootProject.name}-${version}.jar"
    } else {
        taskGroup = rootProject.fatGroup
        artifactType = "fatJar"
        wrapperTask = "templateWrapperFat"
        jarName = "${rootProject.name}-${version}-all.jar"
    }

    task "${getDeployName(map)}" {
        group "$taskGroup"
        dependsOn clean, "$artifactType", "$wrapperTask"
        doLast {
            ssh.run {
                session(remotes.ev3dev) {
                    sshPrint(delegate, "mkdir -p ${project.userJarsPath}/")
                    println "Uploading $jarName"
                    put from: "${project.rootDir}/build/libs/$jarName", into: "${project.userJarsPath}"
                    put from: "${project.rootDir}/build/launcher.sh", into: "$wrapperPath"
                    sshPrint(delegate, "chmod +x \"$wrapperPath\"")
                }
            }
        }
    }
}

////////////////////////
// Create a Basic task
// normal arguments:
//  - name: task name
//  - command: command to be executed
def createBasicTask(name, command) {
    task "$name" {
        group rootProject.baseGroup
        doLast {
            ssh.run {
                session(remotes.ev3dev) {
                    sshPrint(delegate, "$command")
                }
            }
        }
    }
}

/////////////
// GENERAL //
/////////////

createBasicTask("testConnection", "ls")
createBasicTask("pkillJava", "pkill java")
createBasicTask("removePreviousJar", "rm -f ${project.userJarPath(true)} ${project.userJarPath(false)}")

////////////////
// FAT BUILDS //
////////////////

createDeployTask(slim: false)
createRunTask(slim: false, time: true)
createRunTask(slim: false, time: true, brickrun: true)
createRunTask(slim: false, verboseClass: true)
createRunTask(slim: false, sudo: true)
createRunTask(slim: false, jmx: true, brickrun: true)
createRunTask(slim: false, jmx: true, brickrun: true, sudo: true)

/////////////////
// SLIM BUILDS //
/////////////////


createDeployTask(slim: true)
createRunTask(slim: true, time: true)
createRunTask(slim: true, time: true, brickrun: true)
createRunTask(slim: true, verboseClass: true)
createRunTask(slim: true, sudo: true)
createRunTask(slim: true, jmx: true, brickrun: true)
createRunTask(slim: true, jmx: true, brickrun: true, sudo: true)
