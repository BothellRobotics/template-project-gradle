ssh.settings {
    knownHosts = allowAnyHosts
}

remotes {
    ev3dev {
        host = project.brickHost
        user = project.brickUser
        password = project.brickPassword
    }
}

// Directory paths
project.ext.externalJarsPath = "/home/robot/java/libraries"
project.ext.userJarsPath = "/home/robot/java/programs"
project.ext.userWrappersPath = "/home/robot"

project.ext.userJarPath = {
    def suffix = project.slimJar ? "-all" : "";
    return "${project.userJarsPath}/${rootProject.name}-${version}${suffix}.jar";
}

project.ext.userWrapperPath = {
    def suffix = project.slimJar ? "-all" : "";
    return "${project.userWrappersPath}/${rootProject.name}-${version}${suffix}.sh";
}

ext.baseGroup = "ELJ-Basic"
ext.remoteGroup = "ELJ-Remote"

//////////////////
// TASK HELPERS //
//////////////////

/////////////////////////////////
// Get classpath for a Slim JAR
def getSlimClassPath() {
    def dependenciesPath = "${project.userJarPath()}"

    configurations.runtime.each {
        dependenciesPath += ":${project.externalJarsPath}/${it.name}"
    }

    return dependenciesPath
}

////////////////////////////////////////
// Get command line for java execution
// named arguments:
//  - sudo: whether prepend java with sudo
//  - time: whether prepend java with time
//  - brickrun: whether to prepend java with brickrun
//  - verboseClass: whether to debug class loading
//  - jmx: whether to activate the management agent
def getJavaCommand(Map map) {
    def cmdLine = ""
    if (map.sudo) {
        cmdLine += "echo -e \"${project.brickPassword}\" | sudo -S "
    }
    if (map.time) {
        cmdLine += "time "
    }
    if (map.brickrun) {
        cmdLine += "brickrun -- "
    }
    cmdLine += "java "
    if (map.verboseClass) {
        cmdLine += "-Xlog:class+load=info,class+unload=info "
    }
    if (map.jmx) {
        cmdLine += jmxFlags + " "
    }
    if (project.slimJar) {
        cmdLine += "-cp \"${getSlimClassPath()}\" ${project.mainClass}"
    } else {
        cmdLine += "-jar ${project.userJarPath()}"
    }
    return cmdLine
}

////////////////////////////////////////
// Get command line for java execution
// normal arguments:
//  - deploy: whether to prepend the name with deployAnd or remote
// named arguments:
//  - sudo: add Sudo to name
//  - brickrun: add Brick to name
//  - verboseClass: add Classload to name
//  - jmx: add Profiling to name
def getTaskName(Map map, deploy = false) {
    def name = ""

    if (deploy) {
        name += "deployAnd"
    } else {
        name += "remote"
    }

    if (map.jmx) {
        name += "Profiling"
    }
    if (map.sudo) {
        name += "Sudo"
    }
    if (map.brickrun) {
        name += "Brick"
    }
    name += "Run"
    if (map.verboseClass) {
        name += "Classload"
    }
    return name
}

/////////////////////
// WRAPPER SUPPORT //
/////////////////////

task templateWrapperSlim(type: Copy) {
    from "${project.rootDir}/gradle/"
    into "${project.rootDir}/build/"
    include 'launcher-simple.sh.template'
    rename { file -> 'launcher.sh' }
    expand(mode: "jarpath", java_opts: "$jvmFlags", classpath: "${getSlimClassPath()}", mainclass: "$mainClass")
}

task templateWrapperFat(type: Copy) {
    from "${project.rootDir}/gradle/"
    into "${project.rootDir}/build/"
    include 'launcher-simple.sh.template'
    rename { file -> 'launcher.sh' }
    expand(mode: "jarmain", java_opts: "$jvmFlags", jar: "${userJarPath()}")
}

///////////////////
// TASK CREATION //
///////////////////

//////////////////////
// Create a Run task
// named arguments:
//  - sudo: is this a sudo run?
//  - time: is this a timed run?
//  - brickrun: is this a brickrun run?
//  - verboseClass: is class loading debug wanted?
//  - jmx: is JMX agent activation wanted?
def createRunTask(Map map) {
    def onlyRunName = getTaskName(map, false)
    def andDeployName = getTaskName(map, true)

    task "$andDeployName" {
        group "${project.remoteGroup}"
        dependsOn "deploy", "$onlyRunName"
        doLast {}
    }

    task "$onlyRunName" {
        group "${project.remoteGroup}"
        doLast {
            ssh.run {
                session(remotes.ev3dev) {
                    sshPrint(delegate, "${getJavaCommand(map)}}")
                }
            }
        }
    }
}

/////////////////////////
// Create a Deploy task
// named arguments:
//  - slim: deployment type
def createDeployTask(Map map) {
    def wrapperPath = userWrapperPath()

    def artifactType, wrapperTask, jarName
    if (project.slimJar) {
        artifactType = "jar"
        wrapperTask = "templateWrapperSlim"
        jarName = "${rootProject.name}-${version}.jar"
    } else {
        artifactType = "fatJar"
        wrapperTask = "templateWrapperFat"
        jarName = "${rootProject.name}-${version}-all.jar"
    }

    task "deploy" {
        group "${project.remoteGroup}"
        dependsOn clean, "$artifactType", "$wrapperTask"
        doLast {
            ssh.run {
                session(remotes.ev3dev) {
                    sshPrint(delegate, "mkdir -p ${project.userJarsPath}/")
                    println "Uploading $jarName"
                    put from: "${project.rootDir}/build/libs/$jarName", into: "${project.userJarsPath}"
                    put from: "${project.rootDir}/build/launcher.sh", into: "$wrapperPath"
                    sshPrint(delegate, "chmod +x \"$wrapperPath\"")
                }
            }
        }
    }
}

////////////////////////
// Create a Basic task
// normal arguments:
//  - name: task name
//  - command: command to be executed
def createBasicTask(name, command) {
    task "$name" {
        group rootProject.baseGroup
        doLast {
            ssh.run {
                session(remotes.ev3dev) {
                    sshPrint(delegate, "$command")
                }
            }
        }
    }
}

/////////////
// GENERAL //
/////////////

createBasicTask("testConnection", "ls")
createBasicTask("pkillJava", "pkill java")
createBasicTask("removePreviousJar", "rm -f ${project.userJarPath()}")

////////////////
// FAT BUILDS //
////////////////

createDeployTask()
createRunTask(time: true)
createRunTask(time: true, brickrun: true)
createRunTask(verboseClass: true)
createRunTask(sudo: true)
createRunTask(jmx: true, brickrun: true)
createRunTask(jmx: true, brickrun: true, sudo: true)
