ext.ev3devGroup = "Ev3dev management"

//////////////////////////////////////
// Get command for a management task
// normal arguments:
//  - sudo: whether to prepend the command with sudo
//  - command: command to be executed
def getMgmtCommand(sudo, command) {
    def cmdLine = ""
    if (sudo) {
        cmdLine += "echo -e \"${project.brickPassword}\" | sudo -S "
    }
    cmdLine += command
    return cmdLine
}

//////////////////////////////////////
// Create service manipulation tasks
// normal arguments:
//  - sudo: whether to prepend the command with sudo
//  - name: task name
//  - command: command to be executed
def createMgmtTask(sudo, name, command) {
    task "$name" {
        group rootProject.ev3devGroup
        doLast {
            ssh.run {
                session(remotes.ev3dev) {
                    sshPrint(delegate, "${getMgmtCommand(sudo, command)}")
                }
            }
        }
    }
}

//////////////////////////////////////
// Create service manipulation tasks
// normal arguments:
//  - serviceName: systemd name of the manipulated service.
//  - action: systemd action acting on the service
def createServiceTaskSub(serviceName, action) {
    def taskName = action + serviceName.capitalize()
    createMgmtTask(true, taskName, "systemctl $action $serviceName")
}

////////////////////////////////////////
// Create service manipulation tasks
// normal arguments:
//  - serviceName: systemd name of the manipulated service.
def createServiceTask(serviceName) {
    createServiceTaskSub(serviceName, "stop")
    createServiceTaskSub(serviceName, "restart")
}

createServiceTask("bluetooth")
createServiceTask("ntp")
createServiceTask("nmbd")
createServiceTask("brickman")
createMgmtTask(false, "getDebianDistro", "cat /etc/os-release")
createMgmtTask(false, "free", "free")
createMgmtTask(false, "ps", "ps aux | sort -n -k 4")
createMgmtTask(true, "shutdown", "shutdown -h now")
createMgmtTask(false, "ev3devInfo", "ev3dev-sysinfo -m")
