ext.ev3devGroup = "Ev3dev management"

def getMgmtCommand(sudo, command) {
    def cmdLine = ""
    if (sudo) {
        cmdLine += "echo -e \"${project.brickPassword}\" | sudo -S "
    }
    cmdLine += command
    return cmdLine
}

def createMgmtTask(sudo, name, command) {
    def cmd = getMgmtCommand(sudo, command)

    task "$name" {
        group rootProject.ev3devGroup
        doLast {
            ssh.run {
                session(remotes.ev3dev) {
                    sshPrint(delegate, "$cmd")
                }
            }
        }
    }
}

def createServiceTaskSub(serviceName, action) {
    def taskName = action + serviceName.capitalize()
    createMgmtTask(true, taskName, "systemctl $action $serviceName")
}

def createServiceTask(serviceName) {
    createServiceTaskSub(serviceName, "stop")
    createServiceTaskSub(serviceName, "restart")
}

createServiceTask("bluetooth")
createServiceTask("ntp")
createServiceTask("nmbd")
createServiceTask("brickman")
createMgmtTask(false, "getDebianDistro", "cat /etc/os-release")
createMgmtTask(false, "free", "free")
createMgmtTask(false, "ps", "ps aux | sort -n -k 4")
createMgmtTask(true, "shutdown", "shutdown -h now")
createMgmtTask(false, "ev3devInfo", "ev3dev-sysinfo -m")
